"""
    ImageConfig(processor, minval, maxval) 
    ImageConfig(; processor=ColorProcessor(), minval=nothing, maxval=nothing) 

Common configuration component for all [`ImageOutput`](@ref).

`processor` is any [`GridProcessor`](@ref). 
`minval` and `maxval` fields normalise grid values between zero and one, for use 
with Colorshemes.jl. `nothing` values are considered to represent zero or one 
respectively for `minval` and `maxval`, and will not be normalised.
"""
struct ImageConfig{P,Min,Max}
    processor::P
    minval::Min
    maxval::Max
end
function ImageConfig(; 
    init=nothing, font=autofont(), text=TextConfig(; font=font), 
    scheme=Greyscale(), processor=autoprocessor(init, scheme, text), 
    minval=nothing, maxval=nothing, kwargs...
) 
    ImageConfig(processor, minval, maxval)
end

processor(ic::ImageConfig) = ic.processor
minval(ic::ImageConfig) = ic.minval
maxval(ic::ImageConfig) = ic.maxval

"""
Graphic outputs that display the simulation frames as RGB images.

`ImageOutput`s must have a [`Extent`](@ref), [`GraphicConfig`](@ref) 
and [`ImageConfig`](@ref) component, and define a [`showimage`](@ref) method.

See [`GifOutput`](@ref) for an example.

Although the majority of the code is maintained here to enable sharing
and reuse, most `ImageOutput`s are not provided in DynamicGrids.jl to avoid
heavy dependencies on graphics libraries. See
[DynamicGridsGtk.jl](https://github.com/cesaraustralia/DynamicGridsGtk.jl)
and [DynamicGridsInteract.jl](https://github.com/cesaraustralia/DynamicGridsInteract.jl)
for implementations.
"""
abstract type ImageOutput{T} <: GraphicOutput{T} end

"""
    (::Type{<:ImageOutput}(o::Output; 
        frames=frames(o), 
        extent=extent(o), 
        graphicconfig=graphicconfig(o), 
        imageconfig=imageconfig(o), 
        kwargs...)

Generic `ImageOutput` constructor that construct an `ImageOutput` from another `Output`.

"""
function (::Type{F})(o::T; 
    frames=frames(o), extent=extent(o), graphicconfig=graphicconfig(o),
    imageconfig=imageconfig(o), kwargs...
) where F <: ImageOutput where T <: Output 
    F(; 
        frames=frames, running=false, extent=extent, graphicconfig=graphicconfig, 
        imageconfig=imageconfig, kwargs...
    )
end

"""
    (::Type{<:ImageOutput})(init::Union{NamedTuple,AbstractMatrix}; 
        extent=nothing, 
        graphicconfig=nothing, 
        imageconfig=nothing, 
        kwargs...)

Generic `ImageOutput` constructor. Converts an init `AbstractArray` 
to a vector of `AbstractArray`s, uses `kwargs` to constructs required 
[`Extent`](@ref), [`GraphicConfig`](@ref) and [`ImageConfig`](@ref) objects unless
they are specifically passed in using `extent`, `graphicconfig`, `imageconfig`.

All other keyword arguments are passed to these constructors. 

Unused or mis-spelled keyword arguments are ignored.
"""
function (::Type{T})(init::Union{NamedTuple,AbstractMatrix}; 
    extent=nothing, graphicconfig=nothing, imageconfig=nothing, kwargs...
) where T <: ImageOutput
    extent = extent isa Nothing ? Extent(; init=init, kwargs...) : extent
    graphicconfig = graphicconfig isa Nothing ? GraphicConfig(; kwargs...) : extent
    imageconfig = imageconfig isa Nothing ? ImageConfig(; init=init, kwargs...) : imageconfig
    T(; 
        frames=[deepcopy(init)], running=false, extent=extent, 
        graphicconfig=graphicconfig, imageconfig=imageconfig, kwargs...
    )
end

imageconfig(o::Output) = ImageConfig()
imageconfig(o::ImageOutput) = o.imageconfig

processor(o::Output) = processor(imageconfig(o))
minval(o::Output) = minval(imageconfig(o))
maxval(o::Output) = maxval(imageconfig(o))


# Allow constructing a frame with the ruleset passed in instead of SimData
function showframe(frame, o::ImageOutput, data::SimData, f, t)
    showimage(grid2image(o, data, frame, f, t), o, data, f, t)
end

"""
    showimage(image::AbstractArray{AGRB32,2}, output::ImageOutput, f, t)

Show image generated by and `GridProcessor` in an ImageOutput.

# Arguments
- `image`: An array of `Color`
- `output`: the output to define the method for
- `f`: the current frame number
- `t`: the current frame date/time
"""
function showimage end
showimage(image, o, data, f, t) = showimage(image, o, f, t)


# Headless image output
mutable struct NoDisplayImageOutput{T,F<:AbstractVector{T},E,GC,IC} <: ImageOutput{T}
    frames::F
    running::Bool 
    extent::E
    graphicconfig::GC
    imageconfig::IC
end
function NoDisplayImageOutput(; 
    frames, running, extent, graphicconfig, imageconfig, kwargs...
)
    NoDisplayImageOutput(frames, running, extent, graphicconfig, imageconfig)
end
